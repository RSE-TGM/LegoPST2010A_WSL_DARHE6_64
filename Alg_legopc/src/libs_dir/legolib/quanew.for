      SUBROUTINE QUANEW(N,KMAX,U,Z,Y,C,S,IT,JOK,KB,KK,stampa1)
      
* CORREZIONE DELLA SOLUZIONE CON IL METODO QUASI-NEWTON
* (FORMULA DI BROYDEN)

* N    DIMENSIONE DEL VETTORE SOLUZIONE C
* KMAX NUMERO MASSIMO DI PASSI DEL METODO QUASI-NEWTON
* U,Z  MATRICI DI LAVORO, DIMENSIONE(N,KMAX)
* Y,S  VETTORI DI LAVORO, DIMENSIONE N 
*      (SOLUZIONE DELLA ITERAZIONE PRECEDENTE)
* C    VETTORE DIMENSIONE N, IN INPUT CONTIENE LA SOLUZIONE DEL SISTEMA,
*      IN OUTPUT CONTIENE LA SOLUZIONE CORRETTA CON IL METODO QUASI-NEWTON
* IT   NUMERO DI ITERAZIONI
* JOK  =1 LA MATRICE JACOBIANA E' NUOVA - NESSUNA CORREZIONE
*      =0 LA MATRICE JACOBIANA NON E' NUOVA - CORREZIONE DELLA SOLUZIONE
* KB   PARAMETRO DI DIMEZZAMENTO DELLA SOLUZIONE S DELLA ITERAZIONE 
*      PRECEDENTE 
* KK   NUMERO DI PASSI DEL METODO QUASI-NEWTON       
            
      DIMENSION U(N,*),Z(N,*),C(*),S(*),Y(*)
      DATA EPS/0.1E-7/  !EPS tolleranza metodo quasi-newton
      IF(JOK.EQ.1)THEN
        KK=0
      ELSE
        J1=1
        IF(KK.GT.KMAX)J1=KK-KMAX+1
        DO JJ=J1,KK
          J=MOD(JJ,KMAX)
          IF(J.EQ.0)J=KMAX
          DD=SDOT(N,C,1,Z(1,J),1)
          CALL SAXPY(N,DD,U(1,J),1,C,1)
        ENDDO
        IF(IT.GT.1)THEN
          CALL SAXPY(N,-1.,C,1,Y,1)
          IF(KB.GT.0)CALL SSCAL(N,(0.5)**KB,S,1)
          DD=SDOT(N,Y,1,S,1)
          if(stampa1.eq.1)write(55,*)'dd eps',dd,eps  
          IF(ABS(DD).GT.EPS)THEN
          KK=KK+1
          K=MOD(KK,KMAX)
          IF(K.EQ.0)K=KMAX
          CALL SCOPY(N,S,1,U(1,K),1)
          CALL SAXPY(N,-1.,Y,1,U(1,K),1) 
          CALL SSCAL(N,1./DD,U(1,K),1)
          CALL SCOPY(N,S,1,Z(1,K),1)
          DD=SDOT(N,C,1,Z(1,K),1)
          CALL SAXPY(N,DD,U(1,K),1,C,1)
          ENDIF
        ENDIF
      ENDIF
      CALL SCOPY(N,C,1,Y,1)
      CALL SCOPY(N,C,1,S,1)
      RETURN
      END
 

      SUBROUTINE QUANEW1(N,KMAX,U,Z,Y,C,S,IT,JOK,KB,K,stampa1)
      
* CORREZIONE DELLA SOLUZIONE CON IL METODO QUASI-NEWTON
* (FORMULA DI BROYDEN)

* N    DIMENSIONE DEL VETTORE SOLUZIONE C
* KMAX NUMERO MASSIMO DI PASSI DEL METODO QUASI-NEWTON
* U,Z  MATRICI DI LAVORO, DIMENSIONE(N,KMAX)
* Y,S  VETTORI DI LAVORO, DIMENSIONE N 
*      (SOLUZIONE DELLA ITERAZIONE PRECEDENTE)
* C    VETTORE DIMENSIONE N, IN INPUT CONTIENE LA SOLUZIONE DEL SISTEMA,
*      IN OUTPUT CONTIENE LA SOLUZIONE CORRETTA CON IL METODO QUASI-NEWTON
* IT   NUMERO DI ITERAZIONI
* JOK  =1 LA MATRICE JACOBIANA E' NUOVA - NESSUNA CORREZIONE
*      =0 LA MATRICE JACOBIANA NON E' NUOVA - CORREZIONE DELLA SOLUZIONE
* KB   PARAMETRO DI DIMEZZAMENTO DELLA SOLUZIONE S DELLA ITERAZIONE 
*      PRECEDENTE 
* K    NUMERO DI PASSI DEL METODO QUASI-NEWTON       
            
      DIMENSION U(N,*),Z(N,*),C(*),S(*),Y(*)
      DATA EPS/0.1E-7/  !EPS tolleranza metodo quasi-newton
      IF(JOK.EQ.1)THEN
        K=0
      ELSE
        DO J=1,K
          DD=SDOT(N,C,1,Z(1,J),1)
          CALL SAXPY(N,DD,U(1,J),1,C,1)
        ENDDO
        IF(IT.GT.1)THEN
          IF(K.LT.KMAX)THEN
            CALL SAXPY(N,-1.,C,1,Y,1)
            IF(KB.GT.0)CALL SSCAL(N,(0.5)**KB,S,1)
            DD=SDOT(N,Y,1,S,1)
            if(stampa1.eq.1)write(55,*)'dd eps',dd,eps  
            IF(ABS(DD).GT.EPS)THEN
              K=K+1
              CALL SCOPY(N,S,1,U(1,K),1)
              CALL SAXPY(N,-1.,Y,1,U(1,K),1) 
              CALL SSCAL(N,1./DD,U(1,K),1)
              CALL SCOPY(N,S,1,Z(1,K),1)
              DD=SDOT(N,C,1,Z(1,K),1)
              CALL SAXPY(N,DD,U(1,K),1,C,1)
            ENDIF
          ENDIF
        ENDIF        
      ENDIF
      CALL SCOPY(N,C,1,Y,1)
      CALL SCOPY(N,C,1,S,1)
      RETURN
      END
 



