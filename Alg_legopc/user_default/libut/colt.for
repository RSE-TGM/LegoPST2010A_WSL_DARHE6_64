C**********************************************************************
C modulo colt.f
C tipo 
C release 5.3
C data 11/8/95
C reserver @(#)colt.f	5.3
C**********************************************************************
      SUBROUTINE COLTI3(IFO,IOB,DEBL)
      COMMON/COLT00/IBLOC,NE,NU
      CHARACTER*80 DEBL
      CHARACTER*8 IBLOC
      CHARACTER*4 IOB
      CHARACTER*4 MOD
      DATA MOD/'COLT'/
C
C
C---COLLETTORE DI DISTRIBUZIONE A N TUBI
C---CON TEMPERATURA METALLO ,TEMPERATURA ESTERNA E COEFFICIENTE DI
C---SCAMBIO ESTERNO IN EVIDENZA
C
C   ( VERSIONE CON POSSIBILITA' DI INTRODURRE UNA COMPRIMIBILITA' FITTIZIA
C     DEL FLUIDO (se GAMES < 0) AL FINE DI UTILIZZO DEL MODULO COME
C     SMORZATORE DI OSCILLAZIONI NUMERICHE - POLLACHINI 23/12/1993 )
C
C
      CALL COLTI4(IOB,MOD)
C
C
      NSTATI=0
      NUSCIT=3
      NINGRE=2*(NE+NU)+2
      NVAR=NUSCIT+NINGRE
C
C
      WRITE(IFO,2999)IBLOC,IOB,MOD,DEBL
 2999 FORMAT(A,2X,'BL.-',A4,'- **** MODULO ',A4,' - ',A)
C
      WRITE(IFO,3000)IOB
 3000 FORMAT('HCOL',A4,2X,'--UA-- FLUID ENTHALPY IN THE MANIFOLD')
C
      WRITE(IFO,3005)IOB
 3005 FORMAT('PCOL',A4,2X,'--UA-- FLUID PRESSURE IN THE MANIFOLD')
C
      WRITE(IFO,3405)IOB
 3405 FORMAT('TCOL',A4,2X,'--UA-- METAL TEMPERATURE IN THE MANIFOLD')
C
      DO 30 J=1,NE
	WRITE(IFO,3010)J,IOB,J
 3010   FORMAT('WE',I2,A4,2X,
     $    '--IN-- FLOW RATE IN THE ENTERING BRANCH N.',I2)
	WRITE(IFO,3015)J,IOB,J
 3015   FORMAT('HE',I2,A4,2X,
     $    '--IN-- ENTHALPY IN THE ENTERING BRANCH N.',I2)
   30 CONTINUE
C
      DO 40 J=1,NU
	WRITE(IFO,3020)J,IOB,J
 3020   FORMAT('WU',I2,A4,2X,
     $    '--IN-- FLOW RATE IN THE COMING OUT BRANCH N.',I2)
	WRITE(IFO,3025)J,IOB,J
 3025   FORMAT('HU',I2,A4,2X,
     $    '--IN-- ENTHALPY IN THE COMING OUT BRANCH N.',I2,' (W<0)')
   40 CONTINUE
      WRITE(IFO,3415)IOB
 3415 FORMAT('TEST',A4,2X,
     $  '--IN-- FLUID TEMPERATURE OUTSIDE THE MANIFOLD')
      WRITE(IFO,3425)IOB
 3425 FORMAT('GEST',A4,2X,
     $  '--IN-- THERMAL EXCHANGE COEFF. OUTSIDE THE MANIFOLD')
C
      RETURN
      END
      SUBROUTINE COLTI4(IOB,MOD)
      COMMON/COLT00/IBLOC,NE,NU
      CHARACTER*8 IBLOC
      CHARACTER*4 IOB
      CHARACTER*4 MOD
C
      MAX=40
C
   10 WRITE(6,101) MAX
  101 FORMAT(10X,'BRANCH POINT OF FLOW RATES'/
     $       10X,'NUMBER OF ENTERING BRANCHES (01 -',I3,') ?')
      READ(5,*) NE
      IF(NE.LE.0.OR.NE.GT.MAX) THEN
      WRITE(6,102) NE
  102 FORMAT(10X,'ERROR - N.ENT.=',I3,/)
      GOTO 10
      ENDIF
C
   15 WRITE(6,105) MAX
  105 FORMAT(10X,'NUMBER OF COMING OUT BRANCHES (01 -',I3,') ?')
      READ(5,*) NU
      IF(NU.LE.0.OR.NU.GT.MAX) THEN
      WRITE(6,106) NU
  106 FORMAT(10X,'ERROR - N.USC.=',I3,/)
      GOTO 15
      ENDIF
C
      WRITE(IBLOC,1000)MOD,IOB
 1000 FORMAT(2A4)
C
      RETURN
      END
	SUBROUTINE COLTI2(IFUN,VAR,MX1,IV1,IV2,XYU,DATI,ID1,ID2,
     $                    IBL1,IBL2,IER,CNXYU,TOL)
	DIMENSION VAR(MX1,1),XYU(1),DATI(1),CNXYU(1),TOL(1)
	PARAMETER (MAX=40)
        PARAMETER (GAM0=1000.)
	DIMENSION WE(MAX),WU(MAX),HE(MAX),HU(MAX)
C
	COMMON/NORM/P0,H0,T0,W0,RO0,AL0,V0,DP0
C
C---COLLETTORE DI DISTRIBUZIONE A N TUBI
C
	GO TO(100,200),IFUN
C
  100   WRITE(14,500)'NU.TU.EN','NU.TU.US','VOLUME  ',
     1               'MASSA ME','CP MET. ','COND MET',
     1               'SPESSORE','SUP.  ES'
	RETURN
C
C---lettura e memorizzazione dati
C
  200   READ(14,502)
	READ(14,502) FNE,FNU,VOLUME,CM,CPM,COND,SPESS,SUPES
C
  500   FORMAT(3(4X,A8,' =',10X,'*'))
  502   FORMAT(3(14X,F10.2,1X))
C
C______________________________________________
C
       NE=FNE
       NU=FNU
	  HRIF=XYU(IV1  )
	  PRIF=XYU(IV1+1)
	  TEST=XYU(IV1+2+2*NE+2*NU+1)
C
	  ISOLATO=0
	CTMET=CM*CPM
	GAMET=2.*COND/SPESS
	DEQ=(VOLUME+SQRT(VOLUME**2+VOLUME*2.*SPESS*SUPES))*2./SUPES
	DATI(ID2  ) = NE
	DATI(ID2+1) = NU
	DATI(ID2+2) = VOLUME
	DATI(ID2+3) = CTMET
C       DATI(ID2+4) = Sk
C       DATI(ID2+5) = PK
C       DATI(ID2+6) = S
C       DATI(ID2+7) = P
	DATI(ID2+8) = PRIF
	DATI(ID2+10)= GAMET
	DATI(ID2+11)= SUPES
C        DATI(ID2+12)= TM
	DATI(ID2+13)= HRIF
	DATI(ID2+14)= ISOLATO
	DATI(ID2+15)= DEQ
	DATI(ID2+16)= 1000.
C
	ID2 = ID2+16
C
C---costanti di normalizzazione
C
	CNXYU(IV1  ) = H0
	CNXYU(IV1+1) = P0
	CNXYU(IV1+2) = T0
	NT=NE+NU
	I0=IV1+2
	DO J = 1,NT
	JW=2*J-1
	JH=JW+1
	CNXYU(I0+JW) = W0
	CNXYU(I0+JH) = H0
	ENDDO
	CNXYU(I0+2*NT+1) = T0
	CNXYU(I0+2*NT+2) = GAM0 
C
	RETURN
	END
	SUBROUTINE COLTC1(IFUN,AJAC,MX5,IXYU,XYU,IPD,DATI,RNI,IBL1,IBL2)
	DIMENSION AJAC(MX5,*),XYU(*),DATI(*),RNI(*)
C
C---COLLETTORE DI DISTRIBUZIONE A N TUBI
C   CALCOLO RESIDUI
C
	PARAMETER (MAX=40)
	DIMENSION WE(MAX),WU(MAX),HE(MAX),HU(MAX)
C
	COMMON/NORM/P0,H0,T0,W0,RO0,AL0,V0,DP0
	COMMON/PARPAR/NUL(7),ITERT
	COMMON/INTEGR/TSTOP,TEMPO,DTINT,NPAS,CDT
	EXTERNAL COLT
	LOGICAL KREGIM
	COMMON/REGIME/KREGIM
C
	NE=DATI(IPD)
	NU=DATI(IPD+1)
	NVAR=2*(NE+NU)+5
C
	GO TO(100,200,300),IFUN
C
C---topologia jacobiano
C
  100   DO I=1,3
	DO J =1,NVAR
	  AJAC(I,J) = 1.
	END DO
	END DO
	RETURN
C
C---calcolo residui
C
  200   CONTINUE
	CALL COLT(IFUN,IXYU,XYU,IPD,DATI,RNI)
	RETURN
C
C---calcolo jacobiano
C
 300    CONTINUE
      NSTATI =0
      NUSCIT =3
      NINGRE =2*(NE+NU) +2
C
C---------calcolo jacobiano NUMERICO
C
      EPS   =1.E-3
      EPSLIM=1.E-4
      CALL NAJAC(AJAC,MX5,IXYU,XYU,IPD,DATI,RNI,
     $           NSTATI,NUSCIT,NINGRE,EPS,EPSLIM,COLT)
C
      RETURN
      END
	SUBROUTINE COLT(IFUN,IXYU,XYU,IPD,DATI,RNI)
	DIMENSION XYU(*),DATI(*),RNI(*)
C
C---COLLETTORE DI DISTRIBUZIONE A N TUBI
C   CALCOLO RESIDUI
C
	PARAMETER (MAX=40)
        PARAMETER (GAM0=1000.)
	DIMENSION WE(MAX),WU(MAX),HE(MAX),HU(MAX)
C
	COMMON/NORM/P0,H0,T0,W0,RO0,AL0,V0,DP0
	COMMON/PARPAR/NUL(7),ITERT
	COMMON/INTEGR/TSTOP,TEMPO,DTINT,NPAS,CDT
	LOGICAL KREGIM
	COMMON/REGIME/KREGIM
C
C____________ COEFFICIENTI PER DEFINIRE LO SCAMBIO FLUIDO PARETE
C             COPIATI DA GAMTERT
	DATA CCW/4.0125/,CCV/7.495/,AAW/0.0375/,AAV/0.0167E-5/
C
C---calcolo residui
C
C---decodifica dati
C
	NE   =DATI(IPD   )
	NU   =DATI(IPD+1 )
	VOL  =DATI(IPD+2 )
	CTMET=DATI(IPD+3 )
	PRIF = DATI(IPD+8)
	GAMET= DATI(IPD+10)
	SUPES= DATI(IPD+11)
C
	HRIF = DATI(IPD+13)
	ISOLATO=DATI(IPD+14)
	DEQ    =DATI(IPD+15)
C
C---decodifica variabili
C
	HNODO = XYU(IXYU  )
	PNODO = XYU(IXYU+1)
	TMMET = XYU(IXYU+2)
C
	I0=IXYU+2
	DO J = 1,NE
	  JW=2*J-1
	  JH=JW+1
	  WE(J) = XYU(I0+JW)
	  HE(J) = XYU(I0+JH)
	END DO
	I0=I0+2*NE
	DO J = 1,NU
	  JW=2*J-1
	  JH=JW+1
	  WU(J) = XYU(I0+JW)
	  HU(J) = XYU(I0+JH)
	END DO
C
	TEST  = XYU(IXYU+2*(NE+NU)+3)
	GAMES = XYU(IXYU+2*(NE+NU)+4)*GAM0
C
C----- GAMES E' UTILIZZATO COME MOLTIPLICATORE DI DRO/DP (SPELTA !5/06/94)
C----- UTILIZZO DI UNA DRO/DP = 1.E+04 (==> C=100 m/s) PER
C----- INTRODURRE UNA COMPRIMIBILITA' FITTIZIA DEL FLUIDO
	DROSDP = 0.
	IF(GAMES.LT.0.) THEN
	GAMES  = - GAMES
	DROSDP = 1.E-04 * GAMES
	ENDIF
C
C----------------------------flussi dimassa ed energia
C
	SUME=0.
	SUMHE=0.
	SUMU=0.
	DO J= 1,NE
	  IF(WE(J).GT.0.)            THEN
	    SUME=SUME+WE(J)
	    SUMHE=SUMHE+WE(J)*HE(J)
				     ELSE
	    SUMU=SUMU+WE(J)
	  ENDIF
	END DO
	DO J= 1,NU
	  IF(WU(J).LT.0.)            THEN
	    SUME=SUME-WU(J)
	    SUMHE=SUMHE-WU(J)*HU(J)
				     ELSE
	    SUMU=SUMU-WU(J)
	  ENDIF
	END DO
C
	P = PNODO*P0
	H = HNODO*H0
	C = W0*DTINT
C
C------  CASAM    5 Gennaio 1994 -----------
C
C______ PROTEZIONE PER PRESSIONI ED ENTALPIE SBALLATE
C           reintrodotte da vecchio COLN
C
	IF(P.GT.400.E5) THEN
	  P=400.E5
	ELSE IF(P.LT.610.) THEN
	  P=610.
	ENDIF
	IF(H.GT.4200.E3) THEN
	  H=4200.E3
	ELSE IF(H.LT.0.) THEN
	  H=0.
	ENDIF
C----------------------------stazionario o transitorio ?
	IF(KREGIM) GO TO 1000
C----------------------------calcolo transitorio
	IF(ITERT.LE.0.AND.IFUN.EQ.2) THEN
C____ AGGIORNAMENTO ENTROPIA
	  S =SHEV(P,H,1)
	  DATI(IPD+4)=S
C____ AGGIORNAMENTO PRESSIONE
	  DATI(IPD+5)=P
C____ AGGIORNAMENTO TEMPERATURA METALLO
	  DATI(IPD+12)=DATI(IPD+9)
	ENDIF
	  SK =DATI(IPD+4)
	  PK =DATI(IPD+5)
	  TMK=DATI(IPD+12)
C
	  S =SHEV(P,H,1)
	  Y  =YEV(P,S,1)
	  T  =TEV(P,S,1)
	  DELTAP = 1.E-4*P
	  IF(DELTAP.LT.10.)DELTAP=10.
C
C------  CASAM    5 Gennaio 1994 -----------
C
	  IF(P.GE.400.E+05)THEN
	    P1 = P - DELTAP
	    P2=P1
	  ELSE
	    IF(P.LE.600.)THEN
	      P2 = P + DELTAP
	      P1 = P2
	    ELSE
	      P1 = P - DELTAP
	      P2 = P + DELTAP
	    ENDIF
	  ENDIF
C
	  RO1   =ROEV(P1,S,1)
	  DRODP1=1./A2EV(P1,S,1)
	  DRODS1=BEV(P1,S,1)
C
	  RO2   =ROEV(P2,S,1)
	  DRODP2=1./A2EV(P2,S,1)
	  DRODS2=BEV(P2,S,1)
C
	  RO   =(RO1+RO2)/2.
	  DRODP=(DRODP1+DRODP2)/2.
	  DRODS=(DRODS1+DRODS2)/2.
C
C---- IPOTESI DI COMPRIMIBILITA' DEL FLUIDO [C=SQRT(1./DRODP)=100 m/s]
C
	  IF(DRODP.LT.DROSDP) DRODP=DROSDP
C
	 IF(ITERT.LE.0.AND.IFUN.EQ.2) THEN
C____ AGGIORNAMENTO COEFF. DI SCAMBIO
	  CC1=(SUME*W0)**0.8/DEQ
	  IF(Y.LE.0) THEN
C___ ACQUA SOTTORAFFREDDATA
	  GAMI=CC1*(CCW+AAW*T)+10.
	  ELSE IF(Y.LT.1.) THEN
C___ MISCELA
	  GAMI=CC1*100.+10.
	  ELSE IF(Y.GE.1.) THEN
C___ VAPORE SURRISCALDATO
	  GAMI=CC1*(CCV+AAV*P)+10.
	  ENDIF
	  DATI(IPD+16)=GAMI
	ELSE
	  GAMI= DATI(IPD+16)
	ENDIF
C
	  GAMINT =SUPES* GAMET*GAMI/(GAMET+GAMI)
	  GAMEST =SUPES* GAMET*GAMES/(GAMET+GAMES)
C
C______ Integrazione equaz. energia nel metallo
C
	  DEN=(1.+DTINT*(GAMINT+GAMEST)/CTMET)
	  TM = (TMK+DTINT*(GAMINT*T/T0 + GAMEST*TEST)/CTMET)/DEN
	  DATI(IPD+9)=TM
C
C----------------------------residui
C______ Integrazione equaz. energia nel fluido
C
	RNI(1)=VOL*RO*T*(S-SK)/W0/H0-DTINT*((SUMHE-SUME*HNODO)-
     $                            GAMINT*(T-TM*T0)/W0/H0)
C
C
C______ Integrazione equaz. massa  nel fluido
C
	RNI(2)=VOL*(DRODP*(P-PK)+ DRODS*(S-SK))/W0-DTINT*(SUME+SUMU)
C
	RNI(3)= TMMET-TM
C
	RETURN
C
C---calcolo dello stazionario
C
 1000   CONTINUE
	  S=SHEV(P,H,1)
	  RO=ROEV(P,S,1)
	  Y  =YEV(P,S,1)
	  T  =TEV(P,S,1)
C
C          DEQ = 4.*VOL/SUPES           ! vecchia definizione di DEQ
	  CC1=(SUME*W0)**0.8/DEQ
C
	  IF(Y.LE.0) THEN
C___ ACQUA SOTTORAFFREDDATA
	  GAMI=CC1*(CCW+AAW*T)+10.
	  ELSE IF(Y.LT.1.) THEN
C___ MISCELA
	  GAMI=CC1*100.+10.
	  ELSE IF(Y.GE.1.) THEN
C___ VAPORE SURRISCALDATO
	  GAMI=CC1*(CCV+AAV*P)+10.
	  ENDIF
C
	  GAMINT =SUPES* GAMET*GAMI/(GAMET+GAMI)
	  GAMEST =SUPES* GAMET*GAMES/(GAMET+GAMES)
C
	  TM= (GAMINT*T/T0+GAMEST*TEST)/(GAMINT+GAMEST)
	     RNI(1) = SUMHE-SUME*HNODO-GAMINT*(T-TM*T0)/W0/H0
	     RNI(2) = SUME+SUMU
	     RNI(3) = TMMET-TM
C
	DATI(IPD+4)=S
	DATI(IPD+5)=P
	DATI(IPD+6)=S
	DATI(IPD+7)=P
	DATI(IPD+9) =TM
	DATI(IPD+12)=TM
C
	RETURN
	END
C
      SUBROUTINE COLTD1(BLOCCO,NEQUAZ,NSTATI,NUSCIT,NINGRE,SYMVAR,
     $  XYU,IXYU,DATI,IPD,SIGNEQ,UNITEQ,COSNOR,ITOPVA,MXT)
C
C      IMPLICIT DOUBLE PRECISION (A-H, O-Z)                              !DBLE
      CHARACTER*8 BLOCCO, SYMVAR(*), UNITEQ(*)*10, SIGNEQ(*)*50
      DIMENSION XYU(*), DATI(*), COSNOR(*), ITOPVA(MXT,*)
C
      COMMON /NORM/ P0,H0,T0,W0,RO0,AL0,V0,DP0
C
      DO I = 1, NUSCIT
	DO J = 1, NUSCIT+NINGRE
	  ITOPVA (I,J) = 1
	ENDDO
      ENDDO

C
C-----Equazione n. 1 (algebrica):
C
      SIGNEQ(1) = 'FLUID ENERGY CONSERV. INSIDE THE MANIFOLD'
      UNITEQ(1) = 'W'
      COSNOR(1) = W0*H0
C
C-----Equazione n. 2 (algebrica):
C
      SIGNEQ(2) = 'FLUID MASS CONSERV. INSIDE THE MANIFOLD'
      UNITEQ(2) = 'kg'
      COSNOR(2) = W0
C
C-----Equazione n. 3 (algebrica):
C
      SIGNEQ(3) = 'TEMPERATURE OF MANIFOLD METAL WALL COMPUT.'
      UNITEQ(3) = 'K'
      COSNOR(3) = T0
C
      RETURN
      END
