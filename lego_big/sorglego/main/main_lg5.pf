C*********************************************************************
C       Fortran PreCompile:             main_lg5.pf
C       Subsystem:              1
C       Description:
C       %created_by:    lomgr %
C       %date_created:  Thu Apr 22 09:31:07 2004 %
C
C**********************************************************************


C
C Procedura contenente la variabile per identificare la versione
C
      BLOCK DATA BDD_main_lg5_pf
      CHARACTER*80  RepoID
      COMMON /CM_main_lg5_pf / RepoID
      DATA RepoID/'@(#)1,pfsrc,main_lg5.pf,2'/
      END
C**********************************************************************
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C                                                                      C
C           LEGO unificato per singola / doppia precisione             C
C                 e per diverse piattaforme operative                  C
C                                                                      C
C   Attivata versione singola precisione per sistema operativo Unix    C
C                                                                      C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC

      PROGRAM LG5
C
C     CALCOLO DI UN TRANSITORIO
C
C
       include 'lg_parameter.fh'
C
C**************DATI GENERALI
C
      DIMENSION NOSL(N001),NOSUB(N002),NOBLC(N002,2),NUSTA(N002),
     $          NUSCI(N002),NINGR(N002),ISLB(N002),IP(M003),IPVRS(N005),
     $          IPS(M004),IPVRT(N005),IPI(M005),IPVRI(M001),
     $          XY(N003),UU(N004),XYU(N005),DATI(N007),IPDATI(M003),
     $          CNXYU(N005),IPLOT(N003),ISTMP(N003),TOLL(N003),
     $          IVPER(NP00),ITPER(NP00),IPPER(MP01),DATPER(MP02)
      COMMON/C0LG5A/XY,UU,XYU,DATI,CNXYU,TOLL,DATPER,NOSL,NOSUB,NOBLC,
     $              NUSTA,NUSCI,NINGR,ISLB,IP,IPVRS,IPS,IPVRT,IPI,
     $              IPVRI,IPDATI,IPLOT,ISTMP,IVPER,ITPER,IPPER
      COMMON/LGTV01/LGTEMPO,ILEGO,ICOUNT,ICOUNTMX
C
C*********MEMORIA PER LA MATRICE JACOBIANA
C
      DIMENSION IPCJ(M004),ICJ(M006),ICBJ(M006),FJ(M007),ICN(M007),
     $          IRN(M007),IKEEP(M008),IW(M009),W(N003),RHS(N003),
     $          AJAC(N008,N006),RNI(N003),RIGA(N003)
      COMMON/C0LG5B/FJ,W,RHS,AJAC,RNI,RIGA,
     $              IPCJ,ICJ,ICBJ,ICN,IRN,IKEEP,IW
C
C*********MEMORIA PER RIDEFINIZIONE DELLE REALI DIMENSIONI DEL SISTEMA
C
      DIMENSION NQMODU(N002),IPVRSV(N005),
     $          IPVVSS(N003),IPSSVV(N003),RNV(N003),XYV(N003)
      COMMON/C0LG5C/RNV,XYV,NQMODU,IPVRSV,IPVVSS,IPSSVV
C
C*********VARIABILI DI TIPO CARATTERE
C
      CHARACTER*8 VAR(N005),SIVAR(N003),VARI(N004),VARSYS(N003)
      COMMON/C0LG5D/VAR,SIVAR,VARI,VARSYS
C
      CHARACTER*100 NMSIVA(N003)
      CHARACTER*100 NMVARI(N004)
      CHARACTER*80  NMBLOC(N002)
      CHARACTER*8 SIGLA
      DIMENSION IOUSIV(N003),IOUVAR(N004),ITITOL(20)
C
      CHARACTER*100 GIASEL(N005+N002)
      COMMON/VARDE1/NSEL
      COMMON/VARDE2/GIASEL,NMMODU
C
      COMMON/NORM/P0,H0,T0,Q0,R0,AL0,V0,DP0
      COMMON/DIMAJA/NRXJC,NCXJC
      COMMON/PARINT/ITITOL,TFIN,TSTEP,FATOLL,INTERR,TINTER
      COMMON/INTEGR/TSTOP,TEMPO,DTINT,NPAS,CDT,ALFADT
      COMMON/INPAR1/NBL,NEQAL,NEQSIS,NU,NST,NVART,NPVRT,NVRI,NDATI
      COMMON/PARPAR/NPER,NVPLT,NVSTP,NSTPLT,NSTSTP,KPLT,KSTP,ITERT
      COMMON/REGIME/KREGIM
      COMMON/RSTRT1/TREST,IREGIM
      COMMON/FILOTP/FILE21,FILE22
      CHARACTER*50 FILE22,FILE21,FILE10,FILE14
      CHARACTER*32 NMSAVE
      CHARACTER*8  NMMODU
      LOGICAL KREGIM
      LOGICAL RESTAR
C
      REAL TAAA(2)
C
      KREGIM=.FALSE.
      RESTAR=.FALSE.
C
      ICOUNTMX=1000.
C
      NX1=N001
      NX2=N002
      NX3=N003
      NX4=N004
      NX5=N005
      NX6=N006
      NX7=N007
      NX8=N008
      NRXJC=NX8
      NCXJC=NX6
      MX1=M001
      MX2=M002
      MX6=M006
      MX7=M007
      MX8=M008
      MX10=M010
C
C     APERTURA FILE TAVOLE DEL VAPORE
C
      CALL INITSM
C
      FILE10='proc/f04.dat'
      FILE14='f14.dat'
      FILE21='proc/f21.dat'
      FILE22='proc/f22.dat'
C comp. condizionale per comp. con SCO_UNIX
#ifndef SCO_UNIX
      OPEN(UNIT=10,FILE=FILE10,STATUS='OLD',FORM='UNFORMATTED')
      OPEN(UNIT=14,FILE=FILE14,STATUS='OLD')
      OPEN(UNIT=21,FILE=FILE21,STATUS='UNKNOWN',FORM='UNFORMATTED')
      OPEN(UNIT=22,FILE=FILE22,STATUS='UNKNOWN',FORM='UNFORMATTED')
#else
      OPEN(UNIT=10,FILE=FILE10,STATUS='OLD',FORM='UNFORMATTED',
     1     RECL=500000)
C    1     ORGANIZATION='DYNAMIC')
      OPEN(UNIT=14,FILE=FILE14,STATUS='OLD')
      OPEN(UNIT=21,FILE=FILE21,STATUS='UNKNOWN',FORM='UNFORMATTED',
     1     RECL=500000)
C    1     ORGANIZATION='DYNAMIC')
      OPEN(UNIT=22,FILE=FILE22,STATUS='UNKNOWN',FORM='UNFORMATTED',
     1     RECL=500000)
C    1     ORGANIZATION='DYNAMIC')

#endif
C
C   Nota: le UNIT 10, 21 e 22 possedevano l attributo "SHARED"
C
      CALL CLOC (CPUT1)
C
      KS=9
      CALL SSWTCH(KS,LL)
      IF(KS.EQ.9)GO TO 1
      CALL PSWTCH(6)
      CALL SSWTCH(KS,LL)
    1 CONTINUE
C
C     LETTURA DATI DA FILE 10
C
      REWIND 10
      READ(10)NBL,NEQAL,NBL1,NVART,NEQSIS,NEQS1,NPVRT,NU,NU1,NVRI
      READ(10)N,N,NST,SIGLA,(NOSL(I),I=1,NST)
C
      READ(10) (NOSUB(I),NOBLC(I,1),NOBLC(I,2),NUSTA(I),NUSCI(I),
     $         NINGR(I),ISLB(I),NMBLOC(I),I=1,NBL)
      READ(10) (IP(I),I=1,NBL1),(VAR(I),IPVRS(I),I=1,NVART)
      READ(10) (IPS(I),I=1,NEQS1),(SIVAR(I),NMSIVA(I),
     $         IOUSIV(I),I=1,NEQSIS),(IPVRT(I),I=1,NPVRT)
      READ(10) (IPI(I),I=1,NU1),(VARI(I),NMVARI(I),
     $         IOUVAR(I),I=1,NU),(IPVRI(I),I=1,NVRI)
C
      READ(10) P0,H0,T0,Q0,R0,AL0,V0,DP0
      READ(10) (XY(I),I=1,NEQSIS),(UU(I),I=1,NU),(XYU(I),I=1,NVART)
      READ(10) NDATI,(IPDATI(I),I=1,NBL1),(DATI(I),I=1,NDATI),
     $         (CNXYU(I),I=1,NVART),(TOLL(I),I=1,NEQSIS)
      READ(10)IREGIM
C
C     LETTURA DATI PER LA SIMULAZIONE DA FILE 10
C
      READ(10) (ITITOL(I),I=1,20),TFIN,TSTEP,FATOLL,INTERR,TINTER
      READ(10) NPER,NP1,NP2,(IVPER(I),ITPER(I),I=1,NPER),
     $         (IPPER(I),I=1,NP1),(DATPER(I),I=1,NP2)
      READ(10) NVPLT,NVSTP,NSTPLT,NSTSTP,K1,K2,(IPLOT(I),I=1,K1),
     $         (ISTMP(I),I=1,K2),NSEL,(GIASEL(I),I=1,NSEL)
C
C_________ RICOSTRUZIONE DEL VETTORE DEL VETTORE GIASEL( )
C          CONTERRA` LA LISTA DI TUTTE LE VARIABILI REGISTRATE
C          DURANTE IL TRANSITORIO ORDINATE PER BLOCCHI
C          NOME_BLOCCO,VARIABILI REGISTRATE PER QUEL BLOCCO.
C
      DO 71 J=1,NEQSIS
        IOUSIV(J)=0
   71 CONTINUE
      DO 72 K=1,NU
        IOUVAR(K)=0
   72 CONTINUE
      IF (NVPLT .GT.  99998) THEN
        DO 73 J=1,NEQSIS
          IOUSIV(J)=1
   73   CONTINUE
        DO 74 K=1,NU
          IOUVAR(K)=1
   74   CONTINUE
      ELSE
        DO 75 I=1,K1
          IF (IPLOT(I) .GT. 0) THEN
            IOUSIV(IPLOT(I))=1
          ELSE IF (IPLOT(I) .LT. 0) THEN
            IOUVAR(-IPLOT(I))=1
          ENDIF
   75   CONTINUE
      ENDIF
      CALL LIST1(1,NBL,NMBLOC,IP,IPVRS,NMSIVA,IOUSIV,
     $             NMVARI,IOUVAR,GIASEL,NSEL)
C
C__________ LETTURA NOME FILE DI SAVE E TEMPO DEL RESTART
C
      READ(10)NMSAVE,TREST
      IF(IREGIM.EQ.0)WRITE(6,6001)
      IF(IREGIM.EQ.1)WRITE(6,6000)
      IF(IREGIM.NE.2)GO TO 15
      RESTAR=.TRUE.
C
 6000 FORMAT(///,25X,'TRANSITORIO A PARTIRE DA UN REGIME NON ',
     $   'CALCOLATO')
 6001 FORMAT(///,25X,'TRANSITORIO A PARTIRE DA UN REGIME ',
     $   'CALCOLATO')
 6002 FORMAT(///25X,'RIPRESA DI UN TRANSITORIO DAL TEMPO',F10.3,
     $   ' SEC.',//10X,'CONTROLLARE IL TEMPO PREGO'/
     $   10X,'TUTTO O.K. ? ')
 6003 FORMAT(A4)
C
   15 CONTINUE
C
C     COSTRUZIONE DELLA TOPOLOGIA DEL SISTEMA
C     DIFF. - ALG. E FATTORIZZAZIONE AD INDICI
C
      CALL LEGTOP
C
C     CALCOLO DEL TRANSITORIO
C
      CALL LEGDYN(RESTAR)
C
C****REGISTRAZIONE DEL FILE 15 PER UN RESTART
C compilaz. condizionale per comp. con SCO_UNIX
#ifndef SCO_UNIX
      OPEN(UNIT=15,FILE=NMSAVE,STATUS='UNKNOWN',FORM='UNFORMATTED')
#else
      OPEN(UNIT=15,FILE=NMSAVE,STATUS='UNKNOWN',FORM='UNFORMATTED',
     1     RECL=500000)
C    1     ORGANIZATION='DYNAMIC')

#endif
      REWIND 15
C
C****DATI TIPICI DI LEGO
C
      WRITE(15)NBL,NEQAL,NBL1,NVART,NEQSIS,NEQS1,NPVRT,NU,NU1,NVRI
      WRITE(15)N,N,NST,SIGLA,(NOSL(I),I=1,NST)
      WRITE(15)(NOSUB(I),NOBLC(I,1),NOBLC(I,2),NUSTA(I),NUSCI(I),
     $         NINGR(I),ISLB(I),NMBLOC(I),I=1,NBL)
      WRITE(15)(IP(I),I=1,NBL1),(VAR(I),IPVRS(I),I=1,NVART)
      WRITE(15)(IPS(I),I=1,NEQS1),(SIVAR(I),NMSIVA(I),
     $         IOUSIV(I),I=1,NEQSIS),(IPVRT(I),I=1,NPVRT)
      WRITE(15)(IPI(I),I=1,NU1),(VARI(I),NMVARI(I),
     $         IOUVAR(I),I=1,NU),(IPVRI(I),I=1,NVRI)
      WRITE(15)P0,H0,T0,Q0,R0,AL0,V0,DP0
      WRITE(15)(XY(I),I=1,NEQSIS),(UU(I),I=1,NU),(XYU(I),I=1,NVART)
      WRITE(15)NDATI,(IPDATI(I),I=1,NBL1),(DATI(I),I=1,NDATI),
     $         (CNXYU(I),I=1,NVART),(TOLL(I),I=1,NEQSIS)
      IREGIM=2
      WRITE(15)IREGIM
      TSAVE=TREST+TEMPO
      WRITE(15)TSAVE
C
      CALL CLOC (CPUT2)
      TTOT = (CPUT2-CPUT1)
C
      WRITE(6,5322)TTOT
 5322 FORMAT(10X,'TEMPO TOTALE DI CALCOLO  >>>',F12.3,'(SEC)')
C
C_____CHIUSURA DI TUTTI I FILES
C
      CLOSE (10)
      CLOSE (14)
      CLOSE (15)
      CLOSE (22)
      CLOSE (21)
C
      CALL LGSTOP
      STOP
      END
C
C
C
      BLOCK DATA MA2801
      LOGICAL ABORT1,ABORT2,ABORT3
      COMMON /MA30E/ LP,ABORT1,ABORT2,ABORT3
      COMMON /MA30G/ EPS,RMIN
      DATA EPS/1.0E-4/
      DATA LP/6/
      DATA ABORT1/.FALSE./,ABORT2/.FALSE./,ABORT3/.FALSE./
      END
C
C
C
      BLOCK DATA MA2802
      COMMON/MC19B /LP,IFAIL
      DATA LP/6/
      END
C
C
C
      BLOCK DATA MA2803
      LOGICAL LBLOCK,GROW,ABORT1,ABORT2
      COMMON /MA28E/ LP,MP,LBLOCK,GROW
      COMMON /MA28F/ EPS,RMIN,RESID,IRNCP,ICNCP,MINIRN,MINICN,
     $               IRANK,ABORT1,ABORT2
      DATA EPS/1.0E-4/
      DATA LP/6/,MP/6/
      DATA LBLOCK/.TRUE./,GROW/.TRUE./
      DATA ABORT1/.FALSE./,ABORT2/.FALSE./
      END
C
C
C
      BLOCK DATA MA2804
      LOGICAL ABORT
      COMMON /MC23B/ LP,NUMNZ,NUM,LARGE,ABORT
      DATA LP/6/,ABORT/.FALSE./
      END
C            
