C*********************************************************************
C       Fortran Source:             legsis.f
C       Subsystem:              1
C       Description:
C       %created_by:    lomgr %
C       %date_created:  Tue Aug 21 10:27:01 2001 %
C
C**********************************************************************


C
C Procedura contenete la variabile per l'identificazione della versione
C
      BLOCK DATA BDD_legsis_f
      CHARACTER*80  RepoID
      COMMON /CM_legsis_f / RepoID
      DATA RepoID/'@(#)1,fsrc,legsis.f,2'/
      END
C**********************************************************************
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C                                                                      C
C           LEGO unificato per singola / doppia precisione             C
C                 e per diverse piattaforme operative                  C
C                                                                      C
C   Attivata versione singola precisione per sistema operativo Unix    C
C                                                                      C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC

      SUBROUTINE LEGSIS (IGO,NEQSIS,IPCJ,ICJ,NBL,ISLB,NUSTA,NUSCI,
     $   IPDATI,DATI,IP,TN,CDT,XYU,RN,ICONV,SIVAR,MX1,IPVRS,TOLL,
     $   RNO,XYO,JACYES,NITERJ,NJACMX,IPS,IPVRT,XY,UU,NU,IPI,IPVRI,ICBJ,
     $   FJ,RIGA,PX,NEQAL,TEMPO,AJAC,MX5,NOBLC,MX3,RNI,ICDTR,ICDTU,
     $   IGRAD,ICN,IRN,IKEEP,IW,W,RHS,IVECT,JVECT,MXTER,
C___________ NUOVI PARAMETRI E TAVOLE
     $   IPVVSS,IPSSVV,IPVRSV,RNV,NQSISV,XYV,NQMODU,NQALV,NOSUB)
C
      CHARACTER*8 SIVAR(MX1)
      DIMENSION IPCJ(*),ICJ(*),ISLB(*),NUSTA(*),NUSCI(*),IPDATI(*),
     $   DATI(*),IP(*),TN(*),XYU(*),RN(*),ICONV(*),IPVRS(*),TOLL(*),
     $   RNO(*),XYO(*),IPS(*),IPVRT(*),XY(*),UU(*),IPI(*),IPVRI(*),
     $   ICBJ(*),FJ(*),RIGA(*),PX(*),AJAC(MX5,MX5),NOBLC(MX3,2),RNI(*),
     $   ICN(*),IRN(*),IKEEP(*),IW(*),W(*),RHS(*),IVECT(*),JVECT(*),
C___________ NUOVI PARAMETRI E TAVOLE
     $   IPVVSS(*),IPSSVV(*),IPVRSV(*),RNV(*),XYV(*),NQMODU(*),NOSUB(*)
C
      REAL TEMP,FITERT,FJAC,TTOT,TTRES,TTSOL,TTJAC
C      REAL CDTR
      DIMENSION ITITOL(20)
      COMMON/PARPAR/NPER,NVPLT,NVSTP,NSTPLT,NSTSTP,KPLT,KSTP,ITERT
      COMMON/PARINT/ITITOL,TFIN,TSTEP,FATOLL,INTERR,TINTER
      COMMON/RSTRT1/TREST,IREGIM
C
      COMMON/SOLV00/JACSI
      COMMON/FIXPAS/PASFIX
      LOGICAL PASFIX
C
      CHARACTER*80 DESC(10), PSEUDO*8
      CHARACTER*4  IBL,SYITER,SYJACO,SYTCDT,SYTCRE,SYTCSO,SYTCJA
C      CHARACTER*4  SYCDT
      REAL TAAA(2)
      DATA IALG /0/ IPHTIM /1/ IBL /'    '/
      DATA SYITER /'NITE'/ SYJACO /'NJAC'/ SYTCDT /'KTDT'/
      DATA SYTCRE /'KTRE'/ SYTCSO /'KTSO'/ SYTCJA /'KTJA'/
C      DATA SYCDT /'DT/2'/
C
C     DESCRIZIONE DELLE VARIABILI
C
      DATA (DESC(I),I=1,6) /
     $'NITE      NUMERO DI ITERAZIONI PER OGNI PASSO DI INTEGRAZIONE',
     $'NJAC      NUMERO DI JACOBIANI  PER OGNI PASSO DI INTEGRAZIONE',
     $'KTDT      TEMPO DI CALCOLO (SEC) PER OGNI PASSO DI INTEGRAZIONE',
     $'KTRE      TEMPO DI CALCOLO (SEC) DEI RESIDUI PER OGNI PASSO
     $ DI INTEGRAZIONE',
     $'KTSO      TEMPO DI CALCOLO (SEC) DELLE SOLUZIONI PER OGNI
     $ PASSO DI INTEGRAZIONE',
     $'KTJA      TEMPO DI CALCOLO (SEC) DELLO JACOBIANO PER OGNI
     $ PASSO DI INTEGRAZIONE' /
C
C      RISOLVE IL SISTEMA ALG.-DIFF. COL METODO DI NEWTON-RAPHSON SMORZ.
C
C     DESCRIZIONE  DEI VETTORI
C       -MATRICE JACOBIANA -  IPCJ,ICJ,ICBJ,FJ
C       - RESIDUI-SOLUZIONI -   RN ,RNO, XY ,XYO
C       - INGRESSI  -          UU
C       - COEFF.PER EQU.DIFF. -   TN , CDT
C       - TOLLERANZE -           TOLL
C       - MATRICE COLLEG. DIRETTA -  IPVRS
C       - MATRICI COLLEG. INVERSE -  IPS,IPVRT , IPI,IPVRI
C       - DERIVATE  -  PX
C       - VETTORI IN INGRESSO TIPICI- ISLB,NUSTA,NUSCI,IP,XYU,
C                                      IPDATI,DATI,SIVAR
C       - VETTORI PER FATTORIZZAZIONE -  FJ,ICN,IRN,IKEEP,IW,W,
C                                  RHS,IVECT,JVECT
C       - VETTORI DI COMODO  -  RIGA,ICONV,AJAC(MX5,MX5)
C
  999 ITERT=0
      UPIV=.1
      ITER=0
      NITERJ=7
      NJACMX=10
      IF(IGRAD.EQ.1)JACYES=1
      IF(IALG.EQ.1)JACYES=1
      JAC=0
      BETA=.5
      IBETA=0
      VARR=0.
      IVAR=0
      TRES=0.
      TJAC=0.
      TSOL=0.
C
      IT2=0
      IT1=0
C
C_____ CARICAMENTO NEL VETTORE XYV DEI VALORI CONTENUTI IN XY
C
      DO 77 I=1,NEQSIS
      IK=IPSSVV(I)
      IF(IK.GT.0)THEN
         XYV(IK)=XY(I)
      ENDIF
   77 CONTINUE
C
C***           CALCOLO  RESIDUI
    1 ICONVE=1
      CALL CLOC (CPUT1)
C
      CALL LEGRES(NBL,ISLB,NUSTA,NUSCI,IPDATI,DATI,IP,TN,CDT,XYU,RN,
     $       ICONV,SIVAR,MX1,NEQSIS,IPVRS,TOLL,ICONVE,NOBLC,MX3,RNI,
C_________ NUOVI PARAMETRI E TAVOLE
     $            IPVVSS,IPSSVV,RNV,XY,IPS,IPVRT,NOSUB)
C
      CALL CLOC (CPUT2)
      TRES = (CPUT2-CPUT1)
C
C_____ SE NON CI SONO EQUAZIONI DA RISOLVERE HO FINITO IL CALCOLO
C
      IF(NQSISV.EQ.0)GO TO 501
C
      IF(JACSI.EQ.1)JACYES=1
C*************************11 SEPT 88 SPELTA ************************
      IF(ICONVE.EQ.1)GO TO 100
    2 CONTINUE
      VAR=0.
C
      DO 10 I=1,NQSISV
      VAR=VAR + RNV (I)*RNV (I)
   10 CONTINUE
      VAR=SQRT(VAR)
      VAR=VAR/NQSISV
C
      IF(ITERT.EQ.0)GO TO 15
      IF(IVAR.EQ.1)GO TO 15
C
      IF(VAR.GT.VARR)GO TO 65
C
   15 KBETA=0
      DO 20 I=1,NEQSIS
      RNO(I)=RN(I)
      XYO(I)=XY(I)
   20 CONTINUE
C
      VARR=VAR
      JOK=0
C
      IF(IVAR.EQ.1)GO TO 31
      IF(JACYES.EQ.0)GO TO 30
      JACYES=0
      GO TO 35
   30 IF(ITER.LT.NITERJ)GO TO 45
   31 IF(JAC.GE.NJACMX)GO TO 150
      ITER=0
   35 JAC=JAC+1
      JOK=1
C***              CALCOLO  JACOBIANO
      CALL CLOC (CPUT1)
      CALL LEGBL1(NEQSIS,IPS,IPVRT,XYO,XYU)
      CALL LEGBL1(NU    ,IPI,IPVRI,UU,XYU)
C
      CALL LEGJAC(NBL,ISLB,IPDATI,IP,XYU,DATI,IPVRSV,NQMODU,NUSTA,
     $     IPCJ,ICBJ,FJ,CDT,NQSISV,ICJ,RIGA,AJAC,MX5,NOBLC,MX3,RNI,
C******************* TAVOLE *****************************************
     $     NOSUB)
C
C     FATTORIZZAZIONE
C
      NTER=IPCJ(NQSISV+1)-1
C
      IF(IGRAD.EQ.1)GO TO 25
      IF(IGO.EQ.1)GO TO 37
      IF(IALG.EQ.0)GO TO 39
      GO TO 37
C
C      PERTURBAZIONE A GRADINO
C      LA MATRICE JACOBIANA VIENE RIDOTTA ALLE SOLE EQUAZIONI
C      ALGEBRICHE
C
   25 J=0
      IALG=1
C
      DO 28 I=1,NQALV
C
      I1=IPCJ(I)
      I2=IPCJ(I+1)-1
      DO 26 K=I1,I2
      J=J+1
      IRN(J)=I
      ICN(J)=ICJ(K)
   26 CONTINUE
   28 CONTINUE
C
      NQ1=NQALV+1
      DO 29 K=NQ1,NQSISV
C
      J=J+1
      IRN(J)=K
      ICN(J)=K
      FJ(J)=1.
   29 CONTINUE
      J1=J+1
      DO 27 K=J1,NTER
      FJ(K)=0.
   27 CONTINUE
      NTER=J
      GO TO 41
C
C      FATTORIZZAZIONE CON ORDINAMENTO A BLOCCHI
C
   37 J=0
      IALG=0
C
      DO 38 I=1,NQSISV
C
      I1=IPCJ(I)
      I2=IPCJ(I+1)-1
      DO 36 K=I1,I2
      J=J+1
      IRN(J)=I
      ICN(J)=ICJ(K)
   36 CONTINUE
   38 CONTINUE
C
   41 CALL MA28A(NQSISV,NTER,FJ,MXTER,IRN,MXTER,ICN,UPIV,
     $           IKEEP,IW,W,IFLAG)
C
      IF(IFLAG.EQ.0)GO TO 44
      CALL ER28A(IFLAG)
      CALL LGABRT
C
C      FATTORIZAZIONE CONOSCENDO L'ORDINAMENTO A BLOCCHI
C
   39 CONTINUE
      J=0
C
      DO 42 I=1,NQSISV
C
      I1=IPCJ(I)
      I2=IPCJ(I+1)-1
      DO 40 K=I1,I2
      J=J+1
      IVECT(J)=I
      JVECT(J)=ICJ(K)
   40 CONTINUE
   42 CONTINUE
C
      CALL MA28B(NQSISV,NTER,FJ,MXTER,IVECT,JVECT,ICN,
     $           IKEEP,IW,W,IFLAG)
C
      IF(IFLAG.EQ.0)GO TO 44
      CALL ER28A(IFLAG)
      WRITE(6,2002)
 2002 FORMAT(/10X,'RIESEGUO LA FATTORIZZAZIONE CON ORDINAMENTO BLOCCHI')
C
      CALL LEGJAC(NBL,ISLB,IPDATI,IP,XYU,DATI,IPVRSV,NQMODU,NUSTA,
     $     IPCJ,ICBJ,FJ,CDT,NQSISV,ICJ,RIGA,AJAC,MX5,NOBLC,MX3,RNI,
C******************* TAVOLE *****************************************
     $       NOSUB)
      GO TO 37
C
   44 CONTINUE
      CALL CLOC (CPUT2)
      TJAC = (CPUT2-CPUT1)
      IF(ICONVE.EQ.1)GO TO 101
C
   45 ITER=ITER+1
      ITERT=ITERT+1
      CALL CLOC (CPUT1)
C
C     RISOLUZIONE A/I SISTEMA
C
      MTYPE=1
C
      DO 50 I=1,NQSISV
      RHS(I)=RNV(I)
   50 CONTINUE
C
      CALL MA28C(NQSISV,FJ,MXTER,ICN,IKEEP,RHS,W,MTYPE)
C
C______ AGGIORNAMENTO DELLE SOLUZIONI DEL SISTEMA __XYV__
C       E DELLE USCITE COMPLESSIVE DEL MODELLO __XY__
C
      DO 60 I=1,NQSISV
      XYV(I)=XYV(I)+RHS(I)
      JJ=IPVVSS(I)
      XY(JJ)=XYV(I)
   60 CONTINUE
C
      CALL LEGBL1(NEQSIS,IPS,IPVRT,XY,XYU)
      CALL LEGBL1(NU,IPI,IPVRI,UU,XYU)
      IVAR=0
      CALL CLOC (CPUT2)
      TSOL = (CPUT2-CPUT1)
      GO TO 1
C
C     DIFFICOLTA' DI CONVERGENZA
C
   65 CALL SSWTCH(4,LL)
      IF(LL.NE.1)GO TO 70
C
      WRITE(6,1000)VAR,VARR
 1000 FORMAT(///10X,'DIFFICOLTA DI CONVERGENZA N(I)= ',E15.8,1X,
     $   '  N(I-1) =',E15.8)
      GO TO 70
  131 DO 80 I=1,NEQSIS
      RN(I)= RNO(I)
C
      XY(I)=XYO(I)
      JJ=IPSSVV(I)
      IF(JJ.GT.0) THEN
         XYV(JJ)=XY(I)
         RNV(JJ)=RN(I)
      ENDIF
C
   80 CONTINUE
      GO TO 31
   70 IF(JOK.EQ.0)GO TO 131
      KBETA=KBETA+1
C
C     CORREZIONI DELLE SOLUZIONI CON BETA
C
      DO  90 I=1,NEQSIS
         XY(I)= XYO(I) +BETA*(XY(I)-XYO(I))
      JJ=IPSSVV(I)
      IF(JJ.GT.0) THEN
         XYV(JJ)=XY(I)
      ENDIF
   90 CONTINUE
C
      CALL LEGBL1(NEQSIS,IPS,IPVRT,XY,XYU)
      CALL LEGBL1(NU,IPI,IPVRI,UU,XYU)
C
      IF(KBETA.LE.5)GO TO 1
C
C******************SOLUZIONE NON TROVATA
C
      IVAR=1
      IBETA=IBETA+1
C     IF(IBETA.LE.2)GO TO 1
   93 CONTINUE
      WRITE(6,1003)TEMPO
      WRITE(6,7656)VAR
      WRITE(6,3060)
      KE=0
      DO 95 I=1,NEQSIS
      IF(ICONV(I).EQ.0)GO TO 95
      KE=KE+1
      WRITE(6,3061)KE,SIVAR(I),RN(I)
   95 CONTINUE
 3060 FORMAT(//10X,'EQUAZIONI NON SODDISFATTE'//)
 3061 FORMAT(I6,2X,A8,2X,E12.5)
      GO TO 100
 7656 FORMAT(10X,'NORMA RESIDUI= ',E15.8//)
 1003 FORMAT(//10X,'NON RIESCO A RISOLVERE IL SISTEMA ALG-DIFF AL TEMPO'
     $      ,F8.3)
 1001 FORMAT(10X,2I5,E15.8/(1X,I5,E15.8,I5,1X,E15.8))
C
C     IL SISTEMA EST RISOLTO
C
  100 IF(ITERT.NE.0.OR.JACYES.EQ.0)GO TO 101
      JACYES=0
      DO 102 I=1,NEQSIS
      XYO(I)=XY(I)
  102 CONTINUE
      GO TO 35
  101 CONTINUE
      IF(ITERT.EQ.0)GO TO 111
C
C     CALCOLO DELLE DERIVATE RICORDANDO CHE -XN+CDT*F(XN)+TN =RN
C       F(XN)=(RN-TN +XN)*1/CDT
C
      NEQD= NEQSIS-NEQAL
      CDDT=CDT
      IF(IGRAD.EQ.0)GO TO 107
C
C      LA PERTURBAZIONE E' UN GRADINO
C      CALCOLO LE DERIVATE A T=TEMPO+
C
      CDDT=1.
      JACYES=1
C
      CALL LEGRES(NBL,ISLB,NUSTA,NUSCI,IPDATI,DATI,IP,TN,
     $            CDDT,XYU,RN,ICONV,SIVAR,MX1,NEQSIS,IPVRS,
     $            TOLL,ICONVE,NOBLC,MX3,RNI,
C_________ NUOVI PARAMETRI E TAVOLE
     $            IPVVSS,IPSSVV,RNV,XY,IPS,IPVRT,NOSUB)
C
  107 CONTINUE
      DO 110 I=1,NEQD
      K=NEQAL+I
      PX(I)=(RN(K)-TN(I)+XY(K))/CDDT
  110 CONTINUE
      ICDTR=0
      GO TO 112
  111 CONTINUE
      ICDTR=1
C
      IF(ICDTU.EQ.0.OR.PASFIX)GO TO 2
C
  112 CONTINUE
      CALL LEGBL1(NEQSIS,IPS,IPVRT,XY,XYU)
      CALL LEGBL1(NU,IPI,IPVRI,UU,XYU)
C
C*********APERTURA FILE 21 PER REGISTRAZIONE TEMPI DI CALCOLO
C
 501  CONTINUE
C**F21
      NWO=6
C**F21
      IF(IPHTIM.EQ.0)GO TO 500
      IPHTIM=0
      IF(IREGIM.EQ.2)GO TO 505
      TREST=0.
      REWIND 21
      WRITE(21)(ITITOL(I),I=1,20)
C      WRITE(21)NWO,SYITER,IBL,SYJACO,IBL,SYCDT,IBL,SYTCDT,IBL,
C     $   SYTCRE,IBL,SYTCSO,IBL,SYTCJA,IBL
      WRITE(21)NWO,SYITER,IBL,SYJACO,IBL,SYTCDT,IBL,SYTCRE,IBL,
     $   SYTCSO,IBL,SYTCJA,IBL,PSEUDO,NWO,(DESC(I),I=1,NWO)
C**F21
      GO TO 500
  505 IER=0
      CALL FILPOS(21,TREST,NWO,IER)
      IF (IER.NE.0) CALL LGABRT
C
C*********************REGISTRAZIONE FILE 21 TEMPI DI CALCOLO
C
  500 FITERT=ITERT
      FJAC=JAC
      TTOT=(TRES+TSOL)*ITERT+TRES+TJAC*JAC
      TTRES=TRES*(ITERT+1)
      TTSOL=TSOL*ITERT
      TTJAC=TJAC*JAC
      TEMP=TEMPO+TREST
C**F21
C      CDTR=CDT
C      WRITE(21)TEMP,FITERT,FJAC,CDTR,TTOT,TTRES,TTSOL,TTJAC
      WRITE(21)TEMP,FITERT,FJAC,TTOT,TTRES,TTSOL,TTJAC
C**F21
C
      CALL SSWTCH(3,LL)
      IF(LL.NE.1)RETURN
      WRITE(6,3152)TEMPO,ITERT,JAC,CDT
 3152 FORMAT(//10X,'SOL.SIS.ALG.-DIFF. AL TEMPO  ',F8.2/10X,
     $   'ITER.SOL.-RES.',I6/10X,'JACOBIANI',I6/,10X,'CDT=',F8.2//)
      WRITE(6,3163)TRES,TSOL,TJAC
 3163 FORMAT(10X,'TEMPO CALC. RESIDUI=',F10.5,'(SEC)',/10X,
     $   'TEMPO CALC. SOLUZIONI A/I =',F10.5,'(SEC)'/10X,
     $   'TEMPO CALC. E FATT. JACOBIANO=',F10.5,'(SEC)'//)
      RETURN
C
C     SUPERAMENTO NUM.MAX.JACOB.
C
  150 WRITE(6,1005)TEMPO
 1005 FORMAT(///10X,'SUPERATO IL NUMERO MASSIMO DI JACOBIANI DURANTE ',
     $   'LA SOLUZIONE '/10X,'DEL SISTEMA ALG-DIFF AL TEMPO ', F8.3 //)
      GO TO 93
      END
C            
